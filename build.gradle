buildscript {
  // inspired by https://github.com/Netflix/gradle-template
  apply from: file('gradle/buildscript.gradle'), to: buildscript
}

repositories {
  mavenLocal()
  mavenCentral()
}

apply plugin: 'groovy'

apply plugin: 'idea'

apply plugin: 'cobertura'
apply plugin: 'coveralls'

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'bintray'

version = 1
group = 'de.gesellix'

cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs

def dependencyVersions = [
    'commons-lang:commons-lang'                            : '2.6',
    'org.codehaus.groovy:groovy-all'                       : '2.2.2',
    'org.codehaus.groovy.modules.http-builder:http-builder': '0.7.1',
    'org.hamcrest:hamcrest-core'                           : '1.3',
    'xerces:xmlParserAPIs'                                 : '2.6.2',
    'xerces:xercesImpl'                                    : '2.9.1',
    'xml-apis:xml-apis'                                    : '2.0.2'
]

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.2.2'
  compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

  compile 'org.apache.commons:commons-compress:1.8'
  compile 'commons-io:commons-io:2.4'

  compile 'org.slf4j:slf4j-api:1.7.6'

  testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
  testCompile 'co.freeside:betamax:1.1.2'
  testCompile 'org.yaml:snakeyaml:1.10'

  testRuntime 'ch.qos.logback:logback-classic:1.1.2'
}

configurations.all {
  resolutionStrategy {
    failOnVersionConflict()
    force dependencyVersions.collect { k, v -> "$k:$v" }
  }
}

cobertura.coverageFormats = ['html', 'xml']

publishing {
  publications {
    plugin(MavenPublication) {
      from components.java  // jar
    }
  }
}

ext.dry = false
bintray {
  user = project["bintray.user"]
  key = project["bintray.key"]
  publications = ['plugin']
  pkg {
    repo = 'docker-utils'
    //userOrg = 'myorg' // an optional organization name when the repo belongs to one of the user's orgs
    name = 'docker-client'
    desc = 'A Docker client written in Groovy'
    licenses = ['Apache-2.0']
    labels = ['docker']
  }
  dryRun = dry
}
bintrayUpload.dependsOn(":build")

task wrapper(type: Wrapper) {
  gradleVersion = '1.12'
  distributionUrl = 'http://services.gradle.org/distributions/gradle-1.12-all.zip'
}
