import java.text.SimpleDateFormat

buildscript {
  // inspired by https://github.com/Netflix/gradle-template
  apply from: file('gradle/buildscript.gradle'), to: buildscript
}

repositories {
  mavenLocal()
  mavenCentral()
}

apply plugin: 'groovy'

apply plugin: 'idea'

apply plugin: 'jacoco'
apply plugin: 'cobertura'
apply plugin: 'coveralls'

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply plugin: 'versions'

group = 'de.gesellix'

task sourcesJar(type: Jar) {
  dependsOn classes
  classifier 'sources'
  from sourceSets.main.allSource
}

cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs

ext {
  artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
  bintrayDryRun = false
}

def dependencyVersions = [
    'commons-io:commons-io'                                : '1.4',
    'commons-lang:commons-lang'                            : '2.6',
    'commons-logging:commons-logging'                      : '1.1.3',
    'org.apache.httpcomponents:httpclient'                 : '4.3',
    'org.apache.httpcomponents:httpmime'                   : '4.3',
    'org.codehaus.groovy:groovy-all'                       : '2.2.2',
    'org.codehaus.groovy.modules.http-builder:http-builder': '0.7.1',
    'org.hamcrest:hamcrest-core'                           : '1.3',
    'org.slf4j:slf4j-api'                                  : '1.7.7',
    'org.yaml:snakeyaml'                                   : '1.13',
    'xerces:xmlParserAPIs'                                 : '2.6.2',
    'xerces:xercesImpl'                                    : '2.9.1',
    'xml-apis:xml-apis'                                    : '2.0.2'
]

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.2.2'
  compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

  compile 'org.apache.commons:commons-compress:1.8.1'
  compile 'commons-io:commons-io:1.4'

  compile 'org.slf4j:slf4j-api:1.7.7'

  testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
  testCompile 'co.freeside:betamax:1.1.2'
  testCompile 'org.yaml:snakeyaml:1.13'

  testRuntime 'ch.qos.logback:logback-classic:1.1.2'
}

configurations.all {
  resolutionStrategy {
    failOnVersionConflict()
    force dependencyVersions.collect { k, v -> "$k:$v" }
  }
}

cobertura.coverageFormats = ['html', 'xml']

jacocoTestReport.reports {
  xml.enabled = true
  html.enabled = true
}

artifacts {
  archives sourcesJar
}

publishing {
  publications {
    dockerClient(MavenPublication) {
      version artifactVersion
      from components.java
      artifact sourcesJar
    }
  }
}

bintray {
  user = project["bintray.user"]
  key = project["bintray.key"]
  publications = ['dockerClient']
  pkg {
    repo = 'docker-utils'
    //userOrg = 'myorg' // an optional organization name when the repo belongs to one of the user's orgs
    name = 'docker-client'
    desc = 'A Docker client written in Groovy'
    licenses = ['Apache-2.0']
    labels = ['docker']
    version = [
        name: artifactVersion
    ]
  }
  dryRun = bintrayDryRun
}
bintrayUpload.dependsOn build

task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
  distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
