import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.0'
}

subprojects {
    ext {
        artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
        bintrayDryRun = false
    }

    apply plugin: 'groovy'
    apply plugin: 'com.github.ben-manes.versions'

    group = 'de.gesellix'

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    def dependencyVersions = [
            'com.squareup.okio:okio:1.12.0',
            'org.bouncycastle:bcprov-jdk15on:1.56',
            'org.codehaus.groovy:groovy:2.4.10',
            'org.codehaus.groovy:groovy-json:2.4.10',
            'org.slf4j:slf4j-api:1.7.25'
    ]

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            force dependencyVersions
        }
    }
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
    distributionType = Wrapper.DistributionType.ALL
}

apply from: 'debug.gradle'
