buildscript {
  // inspired by https://github.com/Netflix/gradle-template
  apply from: file('gradle/buildscript.gradle'), to: buildscript
}

repositories {
  mavenLocal()
  mavenCentral()
}

apply plugin: 'groovy'

apply plugin: 'idea'

apply plugin: 'cobertura'
apply plugin: 'coveralls'

cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs

def dependencyVersions = [
    'ch.qos.logback:logback-classic'                       : '1.1.2',
    'co.freeside:betamax'                                  : '1.1.2',
    'commons-io:commons-io'                                : '2.4',
    'commons-lang:commons-lang'                            : '2.6',
    'org.apache.commons:commons-compress'                  : '1.8',
    'org.codehaus.groovy:groovy-all'                       : '2.2.2',
    'org.codehaus.groovy.modules.http-builder:http-builder': '0.7.1',
    'org.hamcrest:hamcrest-core'                           : '1.3',
    'org.slf4j:slf4j-api'                                  : '1.6.6',
    'org.spockframework:spock-core'                        : '0.7-groovy-2.0',
    'org.yaml:snakeyaml'                                   : '1.10',
    'xerces:xmlParserAPIs'                                 : '2.6.2',
    'xerces:xercesImpl'                                    : '2.6.2',
    'xml-apis:xml-apis'                                    : '2.0.2'
]

dependencies {
  compile 'org.codehaus.groovy:groovy-all'
  compile 'org.codehaus.groovy.modules.http-builder:http-builder'

  compile 'org.apache.commons:commons-compress'
  compile 'commons-io:commons-io'

  compile 'org.slf4j:slf4j-api'

  testCompile 'org.spockframework:spock-core'
  testCompile 'co.freeside:betamax'
  testCompile 'org.yaml:snakeyaml'

  testRuntime 'ch.qos.logback:logback-classic:1.1.2'
}

configurations.all {
  resolutionStrategy {
    failOnVersionConflict()
    force dependencyVersions.collect { k, v -> "$k:$v" }
  }
}

cobertura.coverageFormats = ['html', 'xml']

task wrapper(type: Wrapper) {
  gradleVersion = '1.12'
  distributionUrl = 'http://services.gradle.org/distributions/gradle-1.12-all.zip'
}
